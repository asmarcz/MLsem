open Mlsem_common
open Annot
open Mlsem_types
open TVOp
open Ast
open Mlsem_utils

(* ===== Logs and errors ===== *)

type log = {
  eid: Eid.t ;
  kind: Checker.error_kind ;
  title: string ;
  descr: Format.formatter -> unit }

let error_priority = function
| Checker.InvalidAnnot -> 20
| Checker.UnboundVar -> 10
| _ -> 0

(* ===== Initial Annot ===== *)

let initial ?(direct_narrowing=true) ?(partition_narrowing=true) refinements e =
  let new_renaming () =
    let s = ref Subst.identity in
    fun dom ->
      let dom' = Subst.domain !s in
      let dom = MVarSet.diff dom dom' in
      let s' = TVOp.refresh ~kind:KInfer dom in
      s := Subst.combine !s s' ; !s
  in
  let new_result () = TVar.mk KInfer None |> TVar.typ in
  let new_param v oty =
    match oty with
    | None -> TVar.mk KInfer (Variable.get_name v) |> TVar.typ |> GTy.mk
    | Some ty -> ty
  in
  let r =
    if partition_narrowing
    then Refinement.Partitioner.from_refinements refinements
    else Refinement.Partitioner.from_refinements (Refinement.Refinements.empty)
  in
  let rec initial r (eid, e) =
    let open IAnnot in
    let left ann = Either.left ann in
    let right ann = Either.right (Rid.create (), ann) in
    let ann = match e with
    | Value ty -> Annot.AValue ty |> left
    | Var _ -> AVar (new_renaming ()) |> right
    | Constructor (_,es) -> AConstruct (List.map (initial r) es) |> right
    | Lambda (dom, v, e) -> ALambda (new_param v dom, initial r e) |> right
    | LambdaRec lst ->
      ALambdaRec (lst |> List.map (fun (dom, v, e) -> new_param v dom, initial r e)) |> right
    | Ite (e, tau, e1, e2) ->
      AIte (initial r e, tau, BMaybe (initial r e1), BMaybe (initial r e2)) |> right
    | App (e1, e2) -> AApp (initial r e1, initial r e2, new_result ()) |> right
    | Operation (_, e) ->  AOp (new_renaming (), initial r e, new_result ()) |> right
    | Projection (_, e) -> AProj (initial r e, new_result ()) |> right
    | TypeCast (e, ty, _) -> ACast (ty, initial r e) |> right
    | TypeCoerce (e, ty, _) -> ACoerce (ty, initial r e) |> right
    | Alt (_,es) -> AAlt (false, List.map (fun e -> Some (initial r e)) es) |> right
    | Let (suggs, v, e1, e2) ->
      let a1 = initial r e1 in
      let tys = Refinement.Partitioner.decomposition_for r v suggs in
      if List.is_empty tys
      then
        let a2 = initial r e2 in
        ALet' (a1, a2) |> right
      else
        (* Format.printf "Part for %a: %a@." Variable.pp v (Utils.pp_list Ty.pp) tys ; *)
        let parts = tys |> List.map (fun ty -> ty, Some ((fun () ->
            let r = Refinement.Partitioner.filter_compatible r v ty in
            initial r e2
          ) |> LazyIAnnot.mk_lazy)) in
        ALet (a1, parts) |> right
    in
    let refinement =
      if direct_narrowing
      then Refinement.Refinements.get refinements eid
      else REnv.empty
    in
    (* Format.printf "Refinements:@.%a@." REnv.pp refinement ; *)
    match ann with
    | Left a -> A (Annot.nc refinement a)
    | Right (rid, ann) -> I { rid ; ann ; refinement }
  in
  initial r e

(* ===== Annotation Reconstruction ===== *)

(* Outcome of refining a node.
   - [Ok (a, ty)]: the sub-derivation [a] is complete and proves type [ty].
   - [Fail]: the node cannot be typed, whatever is done elsewhere.
   - [Subst (ss, a1, a2, r)]: the node needs one of the substitutions of [ss] to
     be applied. [a1] is the derivation to continue with in the branches where
     one of them is applied, [a2] the one for the default branch where none is;
     both are strictly more decided than the derivation given as input, which is
     what makes the search terminate. Each substitution comes with the result it
     was computed for, and [r] records the refinement of the environment under
     which the requirement arose — together they form the branch's coverage. *)
type ('a,'b) result =
| Ok of 'a * GTy.t
| Fail
| Subst of (Subst.t * IAnnot.res) list * 'b * 'b * REnv.t

type cache = { dom : Domain.t ; logs : log list ref ;
               (* When set, a log produced anywhere below the [Alt] that set it
                  carries that [Alt]'s encoding error instead of its own. *)
               alt_err : (Eid.t * (Format.formatter -> unit)) option }

(* Auxiliary *)

(* Insertion sort with respect to the possibly *partial* order [leq]: each
   element is inserted before the first element it is below, so elements that
   [leq] does not relate keep an arbitrary (but deterministic) relative order. *)
let sort_partial leq lst =
  let rec add_elt lst ne =
    match lst with
    | [] -> [ne]
    | e::lst when leq ne e -> ne::e::lst
    | e::lst -> e::(add_elt lst ne)
  in
  List.fold_left add_elt [] (List.rev lst)

let substitute_similar_vars1 mono v t =
  let vs = MVarSet.diff (top_vars t) (MVarSet.union (strict_vars t) (MVarSet.add1 v mono)) in
  let nt = vars_with_polarity1 t |> List.filter_map (fun (v', k) ->
    if MVarSet.mem1 v' vs then
    match k with
    | `Pos -> Some (v', TVar.typ v)
    | `Neg -> Some (v', TVar.typ v |> Ty.neg)
    | `Both -> (* Cases like Bool & 'a \ 'b  |  Int & 'a & 'b *) None
    else None
    )
  in
  Subst.of_list1 nt
let substitute_similar_vars2 mono v row =
  let t = Record.mk' (Row.tail row) [] in
  let vs = MVarSet.diff (top_vars t) (MVarSet.union (strict_vars t) (MVarSet.add2 v mono)) in
  let nrow = vars_with_polarity2 t |> List.filter_map (fun (v', k) ->
    if MVarSet.mem2 v' vs then
    match k with
    | `Pos -> Some (v', RVar.row v)
    | `Neg -> Some (v', (RVar.fty v |> FTy.neg) |> Row.all_fields)
    | `Both -> None
    else None
    )
  in
  Subst.of_list2 nrow
let minimize_new_tvars mono sol =
  let minimize_binding1 sol (v,t) =
    let r = substitute_similar_vars1 mono v t in
    Subst.compose r sol
  in
  let minimize_binding2 sol (v,row) =
    let r = substitute_similar_vars2 mono v row in
    Subst.compose r sol
  in
  let res = List.fold_left minimize_binding1 sol (Subst.bindings1 sol) in
  List.fold_left minimize_binding2 res (Subst.bindings2 sol)

let tally_simpl mono tvars res cs =
  let ntvars s = MVarSet.union tvars (Subst.restrict tvars s |> Subst.intro) in
  let is_better (s1,r1) (s2,r2) =
    let mono2 = List.fold_left MVarSet.union MVarSet.empty
      [ mono ; ntvars s2 ; TVOp.vars r2 ] in
    TVOp.decompose mono (Subst.restrict tvars s1) (Subst.restrict tvars s2)
    |> List.exists (fun s' -> TVOp.tally ~record:false mono2 [(Subst.apply s' r1, r2)] <> [])
  in
  let not_redundant s ss =
    ss |> List.for_all (fun s' -> is_better s' s |> not)
  in
  (* Format.printf "Tallying:@." ;
  cs |> List.iter (fun (a,b) -> Format.printf "%a <= %a@." Ty.pp a Ty.pp b) ; *)
  (* Format.printf "with tvars=%a@." (Utils.pp_list TVar.pp)
    (TVarSet.destruct tvars) ; *)
  (* Format.printf "with env=%a@." Env.pp env ; *)
  tally_const_rows mono cs
  |> !Config.subst_normalization_fun { mono ; tvars ; res }
  |> List.map (minimize_new_tvars (MVarSet.union mono tvars))
  |> List.map (fun s -> s, Subst.apply s res)
  (* Simplify result if it does not impact the domains *)
  |> List.map (fun (s,r) ->
    let mono = MVarSet.union mono (ntvars s) in
    let clean = clean_subst
      ~pos1:Ty.empty ~neg1:Ty.any ~pos2:Row.empty ~neg2:Row.any mono r in
    (Subst.compose clean s, Subst.apply clean r)
  )
  |> Utils.filter_among_others not_redundant
  |> sort_partial (fun (_,r1) (_,r2) -> Ty.leq r1 r2)
  (* |> List.map (fun (s,r) -> Format.printf "%a@.%a@." Sstt.Printer.print_subst' s Ty.pp r ; s,r) *)

let tally_simpl env res cs =
  let mono = TVOp.all_vars KNoInfer in
  let tvars = Env.tvars env in
  let mono2 = MVarSet.proj2 mono in
  let fc = cs |> List.map (fun (a,b) -> FieldCtx.of_tys mono2 [a;b]) |> FieldCtx.merge_many in
  let new_tvars = FieldCtx.fresh_vars fc |> RVarSet.filter (fun rv ->
    let rv = FieldCtx.fvar_of_fresh_var fc rv |> Option.get |> fst in
    MVarSet.mem2 rv tvars
    ) |> MVarSet.of_set2 in
  let tvars = MVarSet.union tvars new_tvars in
  cs |> List.map (fun (a,b) -> (FieldCtx.decorrelate fc a, FieldCtx.decorrelate fc b))
     |> tally_simpl mono tvars (FieldCtx.decorrelate fc res)
     |> List.map (fun (s,r) -> FieldCtx.recombine' fc s, FieldCtx.recombine fc r)

(* Reconstruction algorithm *)

type ('a,'b) result_seq =
| AllOk of 'a list * GTy.t list
| OneFail
| OneSubst of (Subst.t * IAnnot.res) list * 'b list * 'b list * REnv.t

let rec seq (f : 'b -> 'c -> ('a,'b) result) (c : 'a->'b) (lst:('b*'c) list)
  : ('a,'b) result_seq =
  match lst with
  | [] -> AllOk ([],[])
  | (annot,e)::lst ->
    begin match f annot e with
    | Fail -> OneFail
    | Subst (ss,a,a',r) -> OneSubst (ss,a::(List.map fst lst),a'::(List.map fst lst),r)
    | Ok (a,t) ->
      begin match seq f c lst with
      | AllOk (annots, tys) -> AllOk (a::annots, t::tys)
      | OneFail -> OneFail
      | OneSubst (ss, annots, annots',r) ->
        OneSubst (ss, (c a)::annots, (c a)::annots',r) 
      end
    end

let add_to_res a c res =
  begin match res, a with
  | Either.Left lst, None -> Either.Left lst
  | Either.Left lst, Some a -> Either.Left (a::lst)
  | Either.Right (ss,lst,lst',r), _ -> Either.Right (ss,c::lst,c::lst',r)
  end

let dummy_i ann = IAnnot.I { rid = Rid.dummy ; ann ; refinement=REnv.empty }

let rec refine cache env annot (id, e) =
  match annot with
  | IAnnot.A a -> Ok (a, Checker.typeof env a (id, e))
  | IAnnot.I { rid ; ann ; refinement } -> refine_ann refinement cache env (rid, ann) (id, e)
and refine_ann r cache env (rid, annot) (id, e) =
  let open IAnnot in
  let log kind msg descr =
    let eid, kind, msg, descr =
      match cache.alt_err with
      | Some (eid, descr) -> eid, Checker.UntypeableEncoding, "untypeable encoding", descr
      | None -> id, kind, msg, descr
    in
    let log = { eid ; kind ; title=msg ; descr } in
    cache.logs := log::!(cache.logs)
  in
  let env = REnv.refine_env env r in
  let retry_with a = refine cache env a (id, e) in
  let with_res ss = ss |> List.map (fun (s,t) -> (s, Some (rid, t))) in
  let ic ann = I { rid ; ann ; refinement=r } in
  let ac ann = A (Annot.nc r ann) in
  let app res t1 t2 =
    let t1, t2 = GTy.lb t1, GTy.lb t2 in
    let arrow = Arrow.mk t2 res in
    let ss = tally_simpl env res [(t1, arrow)] in
    let ss = if !Config.infer_overload || Ty.is_empty t2 then ss else
      ss |> List.filter (fun (s, _) -> Subst.apply s t2 |> Ty.non_empty)
    in
    log Checker.UntypeableApp "untypeable application" (fun fmt ->
      Format.fprintf fmt "function: @[<h>%a@]@.argument: @[<h>%a@]" Ty.pp t1 Ty.pp t2
      ) ;
    ss
  in
  match e, annot with
  | _, Untyp -> Fail
  | Var v, AVar f ->
    begin match Env.find_opt v env with
    | None ->
      log Checker.UnboundVar "unbound variable"
        (fun fmt -> Format.fprintf fmt "name: %a" Variable.pp v) ;
      Fail
    | Some ty ->
      let tvs, _ = TyScheme.get ty in
      retry_with (ac (Annot.AVar (f tvs)))
    end
  | Constructor (c, es), AConstruct annots when List.length es = List.length annots ->
    begin match refine_seq' cache env (List.combine annots es) with
    | OneFail -> Fail
    | OneSubst (ss, a, a',r) -> Subst (ss,AConstruct a |> ic,AConstruct a' |> ic,r)
    | AllOk (annots,tys) ->
      let doms = Ast.domains_of_construct c Ty.any in
      let tys = List.map GTy.lb tys in
      let ss =
        doms |> List.concat_map (fun doms ->
        tally_simpl env (Ast.construct c tys) (List.combine tys doms)
      ) in
      log Checker.UntypeableConstructor "untypeable constructor" (fun fmt ->
        Format.fprintf fmt "expected: @[<h>%a@]@.given: @[<h>%a@]"
          (Utils.pp_seq (Utils.pp_seq Ty.pp " ; ") " ;; ") doms
          (Utils.pp_seq Ty.pp " ; ") tys
        ) ;
      Subst (with_res ss, Annot.AConstruct annots |> ac, ic Untyp, REnv.empty)
    end
  | Lambda (_,v,e'), ALambda (ty, annot') ->
    let env' = Env.add v (TyScheme.mk_mono ty) env in
    begin match refine' { cache with dom=Domain.empty } env' annot' e' with
    | Ok (annot', _) -> retry_with (ac (Annot.ALambda (ty, annot')))
    | Subst (ss,a,a',r) ->
      Subst (ss,ALambda(ty, a)|>ic,ALambda(ty, a')|>ic,REnv.add v (GTy.lb ty) r)
    | Fail -> Fail
    end
  | LambdaRec lst, ALambdaRec anns when List.length lst = List.length anns ->
    let lst = List.combine lst anns in
    let env' = lst |> List.fold_left
      (fun env ((_,v,_),(ty,_)) -> Env.add v (TyScheme.mk_mono ty) env) env in
    let tys = List.map fst anns in
    let aes = List.map (fun ((_,_,e),(_,a)) -> a,e) lst in
    begin match refine_seq' { cache with dom=Domain.empty } env' aes with
    | OneFail -> Fail
    | OneSubst (ss, a, a',r) ->
      let r = lst |> List.fold_left
        (fun r ((_,v,_),(ty,_)) -> REnv.add v (GTy.lb ty) r) r in
      Subst (ss,ALambdaRec (List.combine tys a) |> ic,
                ALambdaRec (List.combine tys a') |> ic,r)
    | AllOk (annots,tys') ->
      let tys' = List.map GTy.lb tys' in
      let cs = List.combine tys' (List.map GTy.lb tys) in
      let ss = tally_simpl env (Tuple.mk tys') cs in
      log Checker.UntypeableRec "untypeable recursive function" (fun fmt ->
        Format.fprintf fmt "cannot unify the body with self"
        ) ;
      let ok_ann = ac (Annot.ALambdaRec (List.combine tys annots)) in
      Subst (with_res ss, ok_ann, ic Untyp, REnv.empty)
    end
  | Ite (e0,_,e1,e2), AIte (a0,tau,a1,a2) ->
    begin match refine' cache env a0 e0 with
    | Fail -> Fail
    | Subst (ss,a,a',r) -> Subst (ss,AIte(a,tau,a1,a2)|>ic,AIte(a',tau,a1,a2)|>ic,r)
    | Ok (a0, s) ->
      begin match refine_b' cache env (rid, a1) e1 s tau with
      | Fail -> Fail
      | Subst (ss, a1, a1',r) ->
        Subst (ss, AIte(A a0,tau,a1,a2)|>ic, AIte(A a0,tau,a1',a2)|>ic,r)
      | Ok (a1,_) ->
        begin match refine_b' cache env (rid, a2) e2 s (GTy.neg tau) with
        | Fail -> Fail
        | Subst (ss, a2, a2',r) ->
          let to_i = (function
            | Annot.BSkip -> IAnnot.BSkip | Annot.BType a -> IAnnot.BType (A a)) in
          Subst (ss, AIte(A a0,tau,to_i a1,a2)|>ic, AIte(A a0,tau,to_i a1,a2')|>ic,r)
        | Ok (a2,_) -> retry_with (ac (Annot.AIte(a0,tau,a1,a2)))
        end  
      end
    end
  | Alt (settings,_), AAlt (false, anns) ->
    let mask = settings.amask env in
    let anns = List.map2 (fun ann b -> if b then ann else None) anns mask in
    retry_with (ic (AAlt (true, anns)))
  | Alt (settings,es), AAlt (true, anns) ->
    let cache = { cache with alt_err =
      Some (id, fun fmt -> Format.fprintf fmt "%s" (settings.aerror env)) } in
    let rec aux es anns =
      match es, anns with
      | [], [] -> Either.left []
      | e::es, ann::anns ->
        begin match refine_opt' cache env ann e with
        | Fail -> aux es anns |> add_to_res (Some None) None
        | Subst (ss,a,a',r) ->  Either.right (ss,(Some a)::anns,(Some a')::anns,r)
        | Ok (a,_) -> aux es anns |> add_to_res (Some (Some a)) (Some (A a))
        end
      | _, _ -> assert false
    in
    begin match aux es anns with
    | Either.Left lst when List.for_all Option.is_none lst -> Fail
    | Either.Left lst -> retry_with (ac (Annot.AAlt lst))
    | Either.Right (ss,a,a',r) -> Subst (ss,AAlt(true,a)|>ic,AAlt(true,a')|>ic,r)
    end
  | App (e1, e2), AApp (a1,a2,res) ->
    begin match refine_seq' cache env [(a1,e1);(a2,e2)] with
    | OneFail -> Fail
    | OneSubst (ss, [a1;a2], [a1';a2'],r) ->
      Subst (ss,AApp(a1,a2,res)|>ic,AApp(a1',a2',res)|>ic,r)
    | AllOk ([a1;a2],[t1;t2]) ->
      let ss = app res t1 t2 in
      Subst (with_res ss, ac (Annot.AApp(a1,a2,res)), ic Untyp, REnv.empty)
    | _ -> assert false
    end
  | Operation (o,e'), AOp (f,annot',res) ->
    begin match refine' cache env annot' e' with
    | Ok (annot', t') ->
      let tvs, t = Ast.fun_of_operation env o |> TyScheme.get in
      let s = f tvs in
      let t = GTy.substitute s t in
      let ss = app res t t' in
      Subst (with_res ss, ac (Annot.AOp(t,annot',res)), ic Untyp, REnv.empty)
    | Subst (ss,a,a',r) -> Subst (ss,AOp (f,a,res)|>ic,AOp (f,a',res)|>ic,r)
    | Fail -> Fail
    end
  | Projection (p,e'), AProj (annot',res) ->
    begin match refine' cache env annot' e' with
    | Ok (annot', s) ->
      let ty = Ast.domain_of_proj p res in
      let s = GTy.lb s in
      let ss = tally_simpl env res [(s, ty)] in
      log Checker.UntypeableProjection "untypeable projection" (fun fmt ->
        Format.fprintf fmt "argument: @[<h>%a@]" Ty.pp s
        ) ;
      Subst (with_res ss, ac (Annot.AProj annot'), ic Untyp, REnv.empty)
    | Subst (ss,a,a',r) -> Subst (ss,AProj (a,res)|>ic,AProj (a',res)|>ic,r)
    | Fail -> Fail
    end
  | Let (_,v,e1,e2), ALet(annot1,parts) ->
    begin match refine' cache env annot1 e1 with
    | Fail -> Fail
    | Subst (ss,a,a',r) -> Subst (ss,ALet (a,parts)|>ic,ALet (a',parts)|>ic,r)
    | Ok (annot1, s) ->
      let tvs, s = Checker.generalize ~e:e1 env s |> TyScheme.get in
      begin match refine_part_seq' cache env e2 v (tvs,s) parts with
      | OneFail -> Fail
      | OneSubst (ss,p,p',r) -> Subst (ss,ALet(A annot1,p)|>ic,ALet(A annot1,p')|>ic,r)
      | AllOk (p,_) -> retry_with (ac (Annot.ALet (annot1, p)))
      end
    end
  | Let (_,v,e1,e2), ALet'(annot1,annot2) ->
    begin match refine' cache env annot1 e1 with
    | Fail -> Fail
    | Subst (ss,a,a',r) -> Subst (ss,ALet' (a,annot2)|>ic,ALet' (a',annot2)|>ic,r)
    | Ok (annot1, s) ->
      let t = Checker.generalize ~e:e1 env s in
      let env = Env.add v t env in
      begin match refine' cache env annot2 e2 with
      | Fail -> Fail
      | Subst (ss,a2,a2',r) -> Subst (ss,ALet'(A annot1,a2)|>ic,ALet'(A annot1,a2')|>ic,r)
      | Ok (a2,_) -> retry_with (ac (Annot.ALet' (annot1, a2)))
      end
    end
  | TypeCast (e', _, c), ACast (t,annot') ->
    begin match refine' cache env annot' e' with
    | Ok (annot', s) ->
      let lbc, ubc = (GTy.lb s, GTy.lb t), (GTy.ub s, GTy.ub t) in
      let cs = match c with
        | Check -> [lbc;ubc] | CheckStatic -> [lbc] | NoCheck -> [] in
      let ss = tally_simpl env (GTy.lb (GTy.cap t s)) cs in
      log Checker.UntypeableCast "untypeable cast" (fun fmt ->
        if c = Check then
          Format.fprintf fmt "expected: @[<h>%a@]@.given: @[<h>%a@]" GTy.pp t GTy.pp s
        else if c = CheckStatic then
          Format.fprintf fmt "expected: @[<h>%a@]@.given: @[<h>%a@]" Ty.pp (GTy.lb t) Ty.pp (GTy.lb s)
        ) ;
      Subst (with_res ss, ac (Annot.ACast(t,annot')), ic Untyp, REnv.empty)
    | Subst (ss,a,a',r) -> Subst (ss,ACast (t,a)|>ic,ACast (t,a')|>ic,r)
    | Fail -> Fail
    end
  | TypeCoerce (e', _, c), ACoerce (t,annot') ->
    begin match refine' cache env annot' e' with
    | Ok (annot', s) ->
      let lbc, ubc = (GTy.lb s, GTy.lb t), (GTy.ub s, GTy.ub t) in
      let cs = match c with
        | Check -> [lbc;ubc] | CheckStatic -> [lbc] | NoCheck -> [] in
      let ss = tally_simpl env (GTy.lb t) cs in
      log Checker.UntypeableCoercion "untypeable coercion" (fun fmt ->
        if c = Check then
          Format.fprintf fmt "expected: @[<h>%a@]@.given: @[<h>%a@]" GTy.pp t GTy.pp s
        else if c = CheckStatic then
          Format.fprintf fmt "expected: @[<h>%a@]@.given: @[<h>%a@]" Ty.pp (GTy.lb t) Ty.pp (GTy.lb s)
        ) ;
      Subst (with_res ss, ac (Annot.ACoerce(t,annot')), ic Untyp, REnv.empty)
    | Subst (ss,a,a',r) -> Subst (ss,ACoerce (t,a)|>ic,ACoerce (t,a')|>ic,r)
    | Fail -> Fail
    end
  | e, AInter lst ->
    let rec aux dom lst =
      match lst with
      | [] -> Either.left []
      | { coverage ; ann }::lst ->
        let dom', useless =
          match coverage with
          | None -> dom, false
          | Some cov -> Domain.add cov dom, Domain.covers dom cov
        in
        if useless then aux dom lst
        else
          begin match refine_opt' {cache with dom} env ann (id,e) with
          | Fail when !Config.reexplore_failed_domains -> aux dom lst
          | Fail -> aux dom' lst |> add_to_res None { coverage ; ann=None }
          | Subst (ss,a,a',r) ->
            let a, a' = { coverage ; ann=Some a }, { coverage ; ann=Some a' } in
            Either.right (ss,a::lst,a'::lst,r)
          | Ok (a,_) -> aux dom' lst |> add_to_res (Some a) { coverage ; ann=Some (A a) }
          end
    in
    begin match aux cache.dom lst with
    | Either.Left [] -> Fail
    | Either.Left lst -> retry_with (ac (Annot.AInter lst))
    | Either.Right (ss,a,a',r) -> Subst (ss,AInter(a)|>ic,AInter(a')|>ic,r)
    end
  | e, a ->
    Format.printf "e:@.%a@.@.a:@.%a@.@." Ast.pp_e e IAnnot.pp_a a ;
    assert false
(* Decides what to do with a [Subst] requirement. A substitution touching a
   variable of the environment cannot be applied here — the environment would no
   longer agree with the derivation — so the requirement is propagated outwards.
   Otherwise the substitutions only concern variables local to this expression,
   and the node becomes an intersection with one branch per substitution (plus a
   default one), each branch being explored independently.

   Disjointness is also what keeps the cached types of the derivation valid
   under [IAnnot.substitute]: the environment is left untouched, so the types
   already computed for its sub-derivations remain the right ones. *)
and refine' cache env annot e =
  let tvars = Env.tvars env in
  let subst_disjoint s =
    MVarSet.inter (Subst.domain s) tvars |> MVarSet.is_empty
  in
  match refine cache env annot e with
  | Ok (a, ty) -> Ok (a, ty)
  | Fail -> Fail
  | Subst (ss, a1, a2, r) when ss |> List.map fst |> List.for_all subst_disjoint ->
    let default =
      (* Don't add default branch if already covered (also important for error msg) *)
      if ss |> List.exists (fun (s,_) -> Subst.is_identity s)
      then [] else [{ IAnnot.coverage=(Some (None, r)) ; ann=Some a2 }]
    in
    let branches = ss |> List.map (fun (s,res) ->
      let ann = Some (IAnnot.substitute s a1) in
      let coverage = res, REnv.substitute s r in
      { IAnnot.coverage=(Some coverage) ; ann }
      ) in
    let ann = IAnnot.AInter (branches@default) in
    refine' cache env (dummy_i ann) e
  | Subst (ss, a1, a2, r) -> Subst (ss, a1, a2, r)
and refine_opt' cache env ann e =
  match ann with
  | None -> Fail
  | Some ann -> refine' cache env ann e
and refine_b' cache env (rid, bannot) e s tau =
  let with_no_res ss = ss |> List.map (fun (s,_) -> (s, None)) in
  let retry_with bannot = refine_b' cache env (rid, bannot) e s tau in
  match bannot with
  | IAnnot.BMaybe annot ->
    let unsat = Checker.is_type_test_unsat ~tau s in
    if !Config.infer_overload then
      let ss = tally_simpl env Ty.empty [(unsat, Ty.empty)] in
      Subst (with_no_res ss, IAnnot.BSkip, IAnnot.BType annot, REnv.empty)
    else if Ty.is_empty unsat
    then retry_with (IAnnot.BSkip)
    else retry_with (IAnnot.BType annot)
  | IAnnot.BSkip -> Ok (Annot.BSkip, GTy.empty)
  | IAnnot.BType (annot) ->
    begin match refine' cache env annot e with
    | Ok (a, ty) -> Ok (Annot.BType a, ty)
    | Subst (ss,a1,a2,r) -> Subst (ss,IAnnot.BType a1,IAnnot.BType a2,r)
    | Fail -> Fail
    end
and refine_part' cache env e v (tvs, s) (si,annot) =
  match annot with
  | None -> Ok ((si,None), GTy.empty)
  | Some _ when Ty.is_empty (Ty.cap (GTy.ub s) si) -> Ok ((si,None), GTy.empty)
  | Some annot ->
    let t = TyScheme.mk tvs (GTy.cap s (GTy.mk si)) in
    let env = Env.add v t env in
    begin match refine' cache env (LazyIAnnot.get annot) e with
    | Fail -> Fail
    | Subst (ss,a,a',r) ->
      let a, a' = LazyIAnnot.mk a, LazyIAnnot.mk a' in
      Subst (ss,(si,Some a),(si,Some a'),r)
    | Ok (a,ty) -> Ok ((si,Some a),ty)
    end
and refine_seq' cache env lst = seq (refine' cache env) (fun a -> A a) lst
and refine_part_seq' cache env e v s lst =
  seq (fun a () -> refine_part' cache env e v s a)
    (fun (si,annot) -> (si, annot |> Option.map (fun annot -> IAnnot.A annot |> LazyIAnnot.mk)))
    (lst |> List.map (fun a -> (a,())))

let refine env iannot e =
  let cache = { dom = Domain.empty ; logs = ref [] ; alt_err = None } in
  match refine' cache env iannot e with
  | Fail ->
    (* Logs are stored in reverse chronological order, so this keeps
       the most recent log among those of highest priority. *)
    let best = !(cache.logs) |> List.fold_left (fun best log ->
      match best with
      | Some b when error_priority b.kind >= error_priority log.kind -> best
      | _ -> Some log) None
    in
    begin match best with
    | None ->
      let err = { Checker.eid=Eid.dummy ; kind=Checker.InvalidAnnot ;
        title="annotation reconstruction failed" ; descr=None } in
      raise (Checker.Untypeable err)
    | Some log ->
      let err = { Checker.eid=log.eid ; title=log.title ; kind=log.kind ;
        descr=(Some (Format.asprintf "%a" (fun fmt () -> log.descr fmt) ())) } in
      raise (Checker.Untypeable err)
    end
  | Subst _ -> failwith "Top-level environment should not contain an unresolved type variable."
  | Ok (a,_) -> a

let infer ?(direct_narrowing=true) ?(partition_narrowing=true) env r e =
  refine env (initial ~direct_narrowing ~partition_narrowing r e) e
